trylon DirPage
superclass WebServer Page

fields dir path

create: dir path: path
	log: 'New DirPage.  path = "', path, '"'
	this dir = dir
	this path = path


send-body-to: socket
	socket send: header

	# Need to sort the entries, using a Dictionary for that.
	entries = Dictionary new
	for name in dir names
		if name starts-with: "."
			continue
		entries[name] = dir[name]

	for pair in entries pairs
		name = pair key
		entry = pair value
		log: " Sending entry: ", name
		log: "   path: ", path
		if entry is-directory
			send-entry: entry type: "directory" named: name to: socket
		else
			send-entry: entry type: "file" named: name to: socket

	socket send: footer


send-entry: dir type: type named: name to: socket
	url-path = name
	if type == "directory"
		url-path += "/"
	socket send: '<div class="', type, '"><a href="', url-path, '">'
	socket send: "\t", name
	socket send: '</a></div>'
	socket send: ""

header = ¶
	<html>
	<head>
		<title> Trylon source code </title>
		<link rel="stylesheet" href="/index.css" />
	</head>

	<body>
	<div class="directory-contents">


footer = ¶
	</div>
	</body>
	</html>



